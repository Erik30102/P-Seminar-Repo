plugins {
    id 'application'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

project.ext.mainClass = "com.Sandbox.SandboxApplication"

mainClassName = "$mainClass"

project.ext.lwjglVersion = "3.3.4"
project.ext.lwjglNatives = "natives-windows"
project.ext.gsonVersion = "2.11.0"

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":Core")
    
	implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

	implementation "org.lwjgl:lwjgl"
	implementation "org.lwjgl:lwjgl-assimp"
	implementation "org.lwjgl:lwjgl-glfw"
	implementation "org.lwjgl:lwjgl-openal"
	implementation "org.lwjgl:lwjgl-opengl"
	implementation "org.lwjgl:lwjgl-stb"
	runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"

    implementation "com.google.code.gson:gson:$gsonVersion"

    implementation "io.github.spair:imgui-java-app:1.87.6"
}

// wird nicht funktioniren weil die dependencies nicht included sind
jar {
    manifest {
        attributes(
            'Main-Class': "$mainClass"
        )
    }
}

tasks.register('fullBuild', Jar) {
    archiveClassifier = 'fullbuild'

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
    manifest {
        attributes(
            'Main-Class': "$mainClass"
        )
    }
}